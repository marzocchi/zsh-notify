# vim: set nowrap filetype=zsh:
#
# is-terminal-active exits with status 0 when the current shell is running on an
# active terminal window or tab, status 1 when the window or tab is in background
# and status 2 if the current terminal is not supported (eg. it's not iTerm2 nor
# Apple Terminal).
() {
    local plugin_dir

    zstyle -s ':notify:' plugin-dir plugin_dir
    source $plugin_dir/lib

    # run an AppleScript, selected from ./resources by basename given as the
    # first argument, with all other arguments are positional arguments to the
    # script's `on run` handler.
    function run-applescript() {
      local script_name impl

      zstyle -s ':notify:' impl impl 

      script_name="$1"
      shift
      
      "$plugin_dir"/"$impl"/resources/"$script_name".applescript $@ 2>/dev/null
    }

    # exit with code 0 if the terminal window/tab is active, code 1 if inactive.
    function is-terminal-window-active {
        local term

        if [[ "$TERM_PROGRAM" == 'iTerm.app' ]]; then
            term=iterm2
        elif [[ "$TERM_PROGRAM" == 'Apple_Terminal' ]]; then
            term=apple-terminal
        else
          return 2
        fi

        run-applescript is-"$term"-active "$(current-tty)"
    }

    if is-terminal-window-active; then
        if is-inside-tmux; then
            is-current-tmux-pane-active
            return $?
        fi
      else
        return $?
    fi
}
