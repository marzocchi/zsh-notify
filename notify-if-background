# vim: set nowrap filetype=zsh:
#
# Post a notification using either Notification Center using
# terminal-notifier.app or Growl, through growlnotify.
#
# Notification is effectively posted only if the terminal application
# (either Terminal.app or iTerm2) is in background, or if the session tab
# is not active (or in other word when you are not looking at the shell). 
#
# Exits with status 1 if the terminal app is unsupported, or if the
# notifier program is not found.
#
# Exits with status 2 if the terminal is frontmost or the tab is active.
#
# Requirements:
#
# - terminal-notifier.app
#   https://github.com/alloy/terminal-notifier 
#
# - growlnotify
#   http://growl.info/extras.php/#growlnotify

local term_id title icon_switch
local -a otitle oicon
zparseopts t:=otitle -icon:=oicon

icon=$oicon[2]
title=$otitle[2]
message=$(<&0)

[[ -z "$SYS_NOTIFIER" ]] \
  && SYS_NOTIFIER="/usr/local/bin/terminal-notifier"

[[ -z "$GROWL_NOTIFIER" ]] \
  && GROWL_NOTIFIER="/usr/local/bin/growlnotify"

[[ -z "$NOTIFY_NOTIFIER" ]] \
  && NOTIFY_NOTIFIER=$(which notify-send)

if [[ "$TERM_PROGRAM" == 'iTerm.app' ]]; then
  term_id='com.googlecode.iterm2'
elif [[ "$TERM_PROGRAM" == 'Apple_Terminal' ]]; then
  term_id='com.apple.terminal'
elif [[ -x $(which xdotool) ]]; then
  term_id='check_xdotool'
else
  # echo "Unsupported terminal: $TERM_PROGRAM" > /dev/stderr 
  return 1;
fi

if [[ $term_id == 'com.googlecode.iterm2' ]]; then
  TT='tell-iterm2'
  if [[ `$TT <<< "frontmost and (tty of (current session of current terminal) is equal to \"$TTY\")"` == "true" ]]; then
    return 2
  fi
elif [[ $term_id == 'com.apple.terminal' ]]; then
  TT='tell-terminal'
  if [[ `$TT <<<frontmost` == "true" && `$TT -tab <<<selected` == "true" ]]; then
    return 2
  fi
elif [[ $term_id == 'check_xdotool' ]]; then
  ACTIVE_WID=$(xdotool getactivewindow)
  if [[ -z "$TMUX" ]]; then
    if [[ "$ACTIVE_WID" == "$WINDOWID" ]]; then
      return 2
    fi
  else
    ACTIVE_PANE=$(tmux list-windows -F '#{window_active} #{pane_id}' | grep -i '1' | awk '{ print $2 }')
    if [[ ("$TMUX_PANE" == "$ACTIVE_PANE") && ("$ACTIVE_WID" == "$WINDOWID") ]]; then
       return 2
    fi
  fi
else
  # shouldn't actually get up to here
  return 2
fi

#cat /dev/stdin | growlnotify "$@"
# growlnotify "$@" < /dev/stdin


if [[ -x "$SYS_NOTIFIER" ]]; then
  "$SYS_NOTIFIER" -activate "$term_id" -sender "$term_id" -title "$title" -message "$message" 1>/dev/null 
elif [[ -x "$GROWL_NOTIFIER" ]]; then
  [[ ! -z "$icon" ]] && icon_switch="--iconpath $icon"
  echo "$message" | "$GROWL_NOTIFIER" "$title" 
elif [[ -x "$NOTIFY_NOTIFIER" ]]; then
  "$NOTIFY_NOTIFIER" "$title" "$message"
  if (( $+commands[wmctrl] )); then
    if [[ $ZSH_NOTIFY_FOCUS_TERMINAL == "true" ]]; then
      wmctrl -ia $(wmctrl -lp | awk -vpid=$PPID_FIRST '$3==pid {print $1; exit}')
    else
      wmctrl -i -r $(wmctrl -lp | awk -vpid=$PPID_FIRST '$3==pid {print $1; exit}') -b add,demands_attention
    fi
  fi
else
  echo "No notifier program found." > /dev/stderr
  return 1
fi

